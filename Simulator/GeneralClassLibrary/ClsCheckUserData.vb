'This class checks user data
'there are two options
'1: is a user parameter contained in a given interval
'2: is a user interval contained in a given interval

'Status Checked

Imports System.Globalization

Public Class ClsCheckUserData

    Private ReadOnly LM As ClsLanguageManager

    'Contains the value that should be in an interval
    Private MyTxtX As TextBox

    'The user defined interval
    Private MyTxtA As TextBox
    Private MyTxtB As TextBox

    'User defined value
    Private MyX As Decimal

    'The TargetInterval
    Private MyTargetRange As ClsInterval

    'Check Classes
    Private MyCheckIsInterval As ClsCheckIsInterval
    Private MyCheckIsNumeric As ClsCheckIsNumeric

    WriteOnly Property TxtX As TextBox
        Set(value As TextBox)
            MyTxtX = value
        End Set
    End Property

    WriteOnly Property TxtA As TextBox
        Set(value As TextBox)
            MyTxtA = value
        End Set
    End Property

    WriteOnly Property TxtB As TextBox
        Set(value As TextBox)
            MyTxtB = value
        End Set
    End Property

    WriteOnly Property X As Decimal
        Set(value As Decimal)
            MyX = value
        End Set
    End Property

    WriteOnly Property TargetRange As ClsInterval
        Set(value As ClsInterval)
            MyTargetRange = value
        End Set
    End Property

    Public Sub New(TxtX As TextBox, TargetInterval As ClsInterval)
        LM = ClsLanguageManager.LM
        MyTxtX = TxtX
        MyTargetRange = TargetInterval
    End Sub

    Public Sub New(TxtA As TextBox, TxtB As TextBox, TargetInterval As ClsInterval)
        LM = ClsLanguageManager.LM
        MyTxtA = TxtA
        MyTxtB = TxtB
        MyTargetRange = TargetInterval
    End Sub

    Public Sub New(X As Decimal, TargetInterval As ClsInterval)
        LM = ClsLanguageManager.LM
        MyX = X
        MyTargetRange = TargetInterval
    End Sub

    Public Function IsTxtValueAllowed() As Boolean

        Dim MyCheckIsNumeric = New ClsCheckIsNumeric(MyTxtX)

        If MyCheckIsNumeric.IsInputValid() Then

            Dim X As Decimal = MyCheckIsNumeric.NumericValue

            If MyTargetRange.IsNumberInInterval(X) Then
                Return True
            Else
                MessageBox.Show(LM.GetString("TheValue") & X.ToString(CultureInfo.CurrentCulture) &
                            LM.GetString("ValueNotAllowed") & "[" &
                            MyTargetRange.A.ToString(CultureInfo.CurrentCulture) &
                            ", " & MyTargetRange.B.ToString(CultureInfo.CurrentCulture) & "]!")
                MyTxtX.Select()
                Return False
            End If
        Else
            'The error message is generated by MyCheckIsNumeric
            Return False
        End If

    End Function

    Public Function IsValueAllowed() As Boolean
        If MyTargetRange.IsNumberInInterval(MyX) Then
            Return True
        Else
            MessageBox.Show(LM.GetString("TheValue") & MyX.ToString(CultureInfo.CurrentCulture) &
                        LM.GetString("ValueNotAllowed") & "[" &
                        MyTargetRange.A.ToString(CultureInfo.CurrentCulture) &
                        ", " & MyTargetRange.B.ToString(CultureInfo.CurrentCulture) & "]!")
            Return False
        End If
    End Function

    Public Function IsIntervalAllowed() As Boolean

        Dim MyCheckIsInterval As New ClsCheckIsInterval(MyTxtA, MyTxtB)

        If MyCheckIsInterval.IsIntervalValid Then

            Dim UserInterval = New ClsInterval(MyCheckIsInterval.A, MyCheckIsInterval.B)
            If MyTargetRange.IsInterval2PartOfInterval(UserInterval) Then
                Return True
            Else
                MessageBox.Show(LM.GetString("ValueRangeNotAllowed") & " [" &
                       MyTargetRange.A.ToString(CultureInfo.CurrentCulture) &
                       ", " & MyTargetRange.B.ToString(CultureInfo.CurrentCulture) &
                       "] ")
                MyTxtA.Select()
                Return False
            End If

        Else
            'The error message is generated by MyCheckIsInterval
            Return False
        End If


    End Function
End Class
